#!/usr/bin/awk -f
# convert links from a notmarkdown document to index.gph format

# This script only convert the "[ref]:" links.  The rest of the text
# is passed just escaped without other change.

BEGIN {
	# the default is text, "0"
	EXT["gif"] = "g"
	EXT["jpg"] = EXT["jpeg"] = EXT["png"] = "I"
	EXT["cgi"] = EXT["dcgi"] = EXT["gph"] = "1"
}

# match the extension, and default to text/plain

function file_ext_type(s,
	x)
{
	if (s ~ "/$")
		return "1"
	for (x in EXT)
		if (tolower(s) ~ "\\."x"$")
			return EXT[x]
	return "0"
}

function parse_uri(s, link,
	i)
{
	if ((i = index(s, "://")) > 0) {
		link["proto"] = substr(s, 1, i - 1)
		s = substr(s, i + 3)
	}

	if ((i = index(s, "/"))) {
		link["host"] = substr(s, 1, i - 1)
		s = substr(s, i + 1)
	} else {
		link["host"] = s
		s = ""
	}

	if ((i = index(link["host"], ":"))) {
		link["port"] = substr(link["host"], i + 1)
		link["host"] = substr(link["host"], 1, i - 1)
	}

	link["path"] = "/" s
}

function strip_gopher_type(link)
{
	if (link["path"] == "/") {
		link["type"] = "1"
	} else {
		link["type"] = substr(link["path"], 2, 1)
		link["path"] = substr(link["path"], 3)
	}
}

function parse_link(s, link)
{
	link["host"] = "server"
	link["port"] = "port"
	link["desc"] = s

	if (s ~ "^//") {
		parse_uri(substr(s, 3), link)
		link["type"] = file_ext_type(link["path"])
		return
	}

	if (s ~ "^/") {
		link["path"] = s
		link["type"] = file_ext_type(link["path"])
		return
	}

	parse_uri(s, link)
	if (link["proto"] == "telnet") {
		link["type"] = "8"
	} else if (link["proto"] == "gopher") {
		strip_gopher_type(link)
	} else {
		link["type"] = "h"
		link["path"] = "URL:" s
	}
}

match($0, /^\[.*\]:/) {
	LINK["ref"] = substr($1, RSTART + 1, RLENGTH - 3)
	sub(/^\[.*\]: */, "", $0)
	parse_link($0, LINK)
	for (X in LINK)
		gsub(/\|/, "\\|", LINK[X])

	printf("[%s|[%s]: %s|%s|%s|%s]\n",
	  LINK["type"], LINK["ref"], LINK["desc"], LINK["path"],
	  LINK["host"], LINK["port"])

	delete LINK
	next
}

{
	gsub(/\t/, "        ")
	sub(/^[[t]/, "t&")
	print
}
